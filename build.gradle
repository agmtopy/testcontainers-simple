plugins{
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

group 'com.agmtopy'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

test {
    //使用Junit
    useJUnitPlatform()
    //快速失败
    failFast=true
}

jacocoTestReport{
    reports{
        xml.enabled false
        csv.enabled false
        //输出jacoco html文档
        html.destination file("${buildDir}/reports.jacocoHtml")
    }
}

jacocoTestCoverageVerification{
    violationRules{
        rule{
            element ='CLASS'
            //覆盖度检查package
            includes =['com.agmtopy.testcontainers']
            limit{
                //单元覆盖度
                minimum=0.9
            }
        }
    }
}

test.finalizedBy jacocoTestReport
test.finalizedBy jacocoTestCoverageVerification

dependencies {
    /* Spring */
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter:2.2.3.RELEASE'

    /* redis containers*/
    implementation "io.lettuce:lettuce-core:5.1.1.RELEASE"

    /* Test Container */
    testImplementation "org.testcontainers:junit-jupiter:1.14.3"

    /* mysql containers*/
    testImplementation "com.playtika.testcontainers:embedded-mysql:1.79"

    /* mysql containers*/
    testImplementation "com.playtika.testcontainers:testcontainers-common:1.79"

    /* mysql driver */
    testImplementation "mysql:mysql-connector-java:8.0.21"

    /* spring-jdbc */
    testImplementation "org.springframework:spring-jdbc:5.2.8.RELEASE"

    /* tomcat */
    testImplementation "org.apache.tomcat:tomcat-jdbc:9.0.37"

    /* JUnit 5 */
    def junitJupiterVersion = '5.4.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
}